buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath "org.flywaydb:flyway-database-postgresql:11.20.0"
	}
}

plugins {
	id 'java'
	id 'org.springframework.boot' version '4.0.1'
	id 'io.spring.dependency-management' version '1.1.7'
	id "org.flywaydb.flyway" version "11.20.0"
	id 'nu.studer.jooq' version '9.0'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
description = 'eventshuffle'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(25)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-jooq'
	implementation 'org.springframework.boot:spring-boot-starter-validation'

	implementation 'org.postgresql:postgresql:42.7.3'

	implementation 'org.flywaydb:flyway-core'
	implementation 'org.flywaydb:flyway-database-postgresql:11.20.0'

	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	jooqGenerator 'org.postgresql:postgresql:42.7.3'
}

flyway {
	url = 'jdbc:postgresql://localhost:5432/eventshuffle_db'
	user = 'demo_user'
	password = 'demo_password'
	schemas = ['eventschema']
}

jooq {
	version = '3.19.29'
	configurations {
		main {
			generationTool {
				jdbc {
					driver = 'org.postgresql.Driver'
					url = 'jdbc:postgresql://localhost:5432/eventshuffle_db'
					user = 'demo_user'
					password = 'demo_password'
				}
				generator {
					name = 'org.jooq.codegen.DefaultGenerator'
					database {
						name = 'org.jooq.meta.postgres.PostgresDatabase'
						inputSchema = 'eventschema'
					}
					generate {
						records = true
						immutablePojos = true
						fluentSetters = true
					}
					target {
						packageName = 'com.example.jooq'
						directory = 'src/jooq/main'
					}
				}
			}
		}
	}
}

sourceSets {
	main {
		java {
			srcDirs += 'src/jooq/main'
		}
	}
}

tasks.named('test') {
	useJUnitPlatform()
}
